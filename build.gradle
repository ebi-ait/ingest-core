import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    integrationImplementation.extendsFrom testImplementation
    integrationRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

test {
    useJUnitPlatform()
}

task verify {
    dependsOn test
    dependsOn integrationTest
    integrationTest.mustRunAfter test
}

tasks.withType(Test) {
    testLogging {
        showStackTraces = true
        exceptionFormat = TestExceptionFormat.FULL
    }
}

dependencies {
    ['actuator', 'data-rest', 'hateoas', 'security', 'web', 'webflux'].forEach {
        implementation "org.springframework.boot:spring-boot-starter-${it}"
    }
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation('org.springframework.boot:spring-boot-starter-amqp') {
        exclude group: 'org.springframework.retry', module: 'spring-retry'
    }
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb') {
        exclude group: 'org.mongodb', module: 'mongodb-driver'
    }
    implementation 'org.springframework.data:spring-data-rest-hal-browser'

    implementation 'org.springframework.retry:spring-retry:1.3.1'
    implementation 'org.springframework:spring-aspects:5.3.13'

    implementation 'org.apache.httpcomponents:httpcore'
    implementation 'org.apache.httpcomponents:httpmime'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.10.0.pr3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0.pr3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.0.pr3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.flipkart.zjsonpatch:zjsonpatch:0.4.8'
    implementation 'com.auth0:auth0-spring-security-api:1.2.5'
    implementation 'com.auth0:java-jwt:3.8.2'
    implementation 'com.auth0:jwks-rsa:0.8.3'
    implementation 'javax.mail:mail:1.4.1'

    implementation 'org.jolokia:jolokia-core'
    implementation 'org.zalando:problem:0.23.0'

    implementation 'org.mongodb:mongo-java-driver:3.11.2'
    implementation('com.github.mongobee:mongobee:0.13') {
        exclude group: 'org.mongodb', module: 'mongo-java-driver'
    }

    compile 'org.projectreactor:reactor-spring:1.0.1.RELEASE'
    compile 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.assertj', module: 'assertj-core'
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.mockito', module: 'mockito-core'
    }
    testImplementation 'org.assertj:assertj-core:3.10.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.14.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testImplementation('org.mockito:mockito-junit-jupiter:3.0.0') {
        exclude group: 'org.mockito', module: 'mockito-core'
    }
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.4.0")
    testImplementation("com.squareup.okhttp3:okhttp:4.4.0")
    testImplementation("org.jetbrains.kotlin:kotlin-stdlib:1.3.61")

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testRuntimeOnly 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
}
